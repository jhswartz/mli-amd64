[ . x86 ]

Magic:
	90

Start:
	# Gather argc and skip argv[0].
	59					# pop rcx
	5e					# pop rsi

	# Abort if argc is not 2.
	83 f9 02				# cmp 2 ecx
	75 [ rel8 Abort ]			# jnz Abort

ParseSeconds:
	# Clear the buffer (rax) and accumulator (rdx).
	33 c0					# xor eax eax
	99					# cdq

	# Parse argv[1].
	5e					# pop rsi

NextByte:
	# Read the next character.
	ac					# lodsb

	# NUL terminator?
	84 c0					# test al al
	74 [ rel8 Sleep ]			# jz Sleep

	# Abort if it's not a digit.
	2c 30					# sub '0' al
	3c 09					# cmp 9 al
	77 [ rel8 Abort ]			# ja Abort

	# Accumulate.
	6b d2 0a				# imul 10 edx
	03 d0					# add eax edx

	# Continue.
	eb [ rel8 NextByte ]			# jmp NextByte

Sleep:
	# nanosleep({ .tv_sec = seconds, .tv_usec = 0 }, NULL);
	31 f6					# xor esi esi
	56 52					# push { seconds 0 }
	48 89 e7				# mov rsp rdi
	6a 23 58				# mov SYS_NANOSLEEP rax
	0f 05					# syscall

Quit:
	6a 00					# push EXIT_SUCCESS
	eb [ rel8 Exit ]			# jmp Exit

Abort:
	6a 01					# push EXIT_FAILURE

Exit:
	5f					# pop rdi
	6a 3c 58				# mov SYS_EXIT rax
	0f 05					# syscall
