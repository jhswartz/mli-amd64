[ . x86 ]

Start:
	# Gather argc and skip argv[0].
	5a				# pop rdx
	5f				# pop rdi

	# Abort if no arguments were supplied.
	ff ca				# dec edx
	74 [ rel8 Abort ]		# jz Abort

Open:
	# open(argv[1], O_RDONLY);
	48 8b 3c 24			# mov argv[1] rdi
	33 f6				# mov O_RDONLY esi
	6a 02 58			# mov SYS_OPEN rax
	0f 05				# syscall
	
	# Abort on failure.
	48 85 c0			# test rax rax
	78 [ rel8 Abort ]		# js Abort
	
	# Preserve the reduced argument count.
	52				# push rdx

	# Stash the descriptor for mmap(2).
	50 				# push rax

GetFileSize:
	# Borrow 144 bytes from the stack for status.
	48 8d b4 24 70 ff ff ff		# lea (rsp - 144) rsi

	# fstat(descriptor, &status);
	50 5f				# mov rax rdi
	6a 05 58			# mov SYS_FSTAT rax
	0f 05				# syscall
	
	# Abort on failure.
	48 85 c0			# test rax rax
	75 [ rel8 Abort ]		# jnz Abort
	
MapFile:
	# mmap(NULL, size, prot, flags, descriptor, 0);
	4d 31 c9			# mov 0 r9
	41 58				# mov descriptor r8
	6a 02 41 5a			# mov MAP_PRIVATE r10
	6a 07 5a			# mov PROT_READ|PROT_WRITE|PROT_EXEC rdx
	48 8b 76 30			# mov status.st_size rsi
	31 ff				# mov NULL rdi
	6a 09 58			# mov SYS_MMAP rax
	0f 05				# syscall
	
	# Abort on failure.
	48 85 c0			# test rax rax
	78 [ rel8 Abort ]		# js Abort
	
RunProgram:
	ff e0				# jmp rax

Abort:
	6a 01 5f			# mov EXIT_FAILURE rdi
	6a 3c 58			# mov SYS_EXIT rax
	0f 05				# syscall
